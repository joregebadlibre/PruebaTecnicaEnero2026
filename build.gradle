plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.openapi.generator' version '7.6.0'
}

group = 'com.pruebatecnica'
version = '0.0.1-SNAPSHOT'


repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'io.swagger.core.v3:swagger-annotations-jakarta:2.2.22'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    runtimeOnly 'org.postgresql:postgresql'

    implementation 'org.apache.pdfbox:pdfbox:2.0.30'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'com.h2database:h2'
}

tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/openapi.yaml"
    outputDir = "$buildDir/generated/openapi"
    apiPackage = 'com.pruebatecnica.openapi.api'
    modelPackage = 'com.pruebatecnica.openapi.model'
    invokerPackage = 'com.pruebatecnica.openapi.invoker'
    configOptions = [
            interfaceOnly: 'true',
            useSpringBoot3: 'true',
            useTags: 'true',
            dateLibrary: 'java8'
    ]
    globalProperties = [
            apis: '',
            models: ''
    ]
}

sourceSets {
    main {
        java {
            srcDirs = [
                    "src/main/java",
                    "$buildDir/generated/openapi/src/main/java"
            ]
        }
    }
}

tasks.named('compileJava') {
    dependsOn tasks.named('openApiGenerate')
}
